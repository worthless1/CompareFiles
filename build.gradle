plugins {
    id 'java'
}

group 'com.dy.comparedocs'
version '1.0-SNAPSHOT'


repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    // https://mvnrepository.com/artifact/org.apache.logging.log4j/log4j-core
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.14.1'
    // https://mvnrepository.com/artifact/io.github.java-diff-utils/java-diff-utils
    implementation group: 'io.github.java-diff-utils', name: 'java-diff-utils', version: '4.9'
}

jar {
    manifest {
        attributes('Main-Class': 'com.dy.comparedocs.Main',
                'Class-Path': configurations.runtimeClasspath.files.collect { "lib/" + it.getName() }.join(' ')
        )
    }
}

test {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

task dist {
    group 'build'
    dependsOn build
    doLast {
        mkdir 'build/dist'
        copy {
            from('build/libs')
            into('build/dist')
            include '*.jar'
        }
        copy {
            from(sourceSets.main.runtimeClasspath)
            into('build/dist/lib')
            include '*.jar'
        }
    }
}